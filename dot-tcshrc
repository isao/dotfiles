#
# env vars
#
set path=(/{,s}bin /usr/{,s,local/}bin)
if(-d /opt/local) set path=(/opt/local/{,s,apache2/}bin $path)
if(-d /opt/brew) then
  set path=(/opt/brew/{,s}bin $path)
  if(-d /opt/brew/Cellar/gems) then
    setenv GEM_HOME `ls /opt/brew/Cellar/gems | sort -n | head -1`
  endif
endif

setenv EDITOR `which bbedit..sh nano vim vi | grep '^/' | head -1`
setenv CVS_RSH ssh
setenv CVSIGNORE '.DS_Store,._?*,.FBC*,.#?*'
setenv GREP_OPTIONS '--binary-files=without-match'
setenv LESS '--tabs=2 -iFMRX'
setenv RSYNC_RSH $CVS_RSH

if(-X node && -d /opt/brew/lib/node) setenv NODE_PATH /opt/brew/lib/node


#
# shell interactivity
#
if($?USER && $?prompt && -l $HOME/.tcshrc) then
  #this file is a symlink from ~/.tcshrc to <somedir>/dot-tcsrc
  #source <somedir>/tcsh/*.tcsh
  set _mysrcs = `readlink ~/.tcshrc`
  set _mysrcs = $_mysrcs:h/tcsh
  source $_mysrcs/aliases.tcsh
  source $_mysrcs/terminal.tcsh
  source $_mysrcs/completions.tcsh
  source $_mysrcs/y.tcsh
  unset $_mysrcs
endif

# see also
# /usr/share/tcsh/examples/tcsh.defaults
# http://www.opensource.apple.com/source/tcsh/tcsh-63.2/tcsh/complete.tcsh
