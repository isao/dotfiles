#
# env vars
#
set path=(/{,s}bin /usr/{,s,local/}bin)

#my scripts
if(-d /opt/isao) set path=(/opt/isao $path)

#macports
#if(-d /opt/local) set path=(/opt/local/{,s,apache2/}bin $path)

#homebrew
if(-d /opt/brew) then
  if(-x /opt/brew/bin/node) then
    setenv NODE_PATH /opt/brew/lib/node_modules
  endif
  set path=(/opt/brew/bin $path)
endif

setenv EDITOR `which bbedit..sh nano vim vi | grep '^/' | head -1`
setenv CVS_RSH ssh
setenv CVSIGNORE '.DS_Store,._?*,.FBC*,.#?*'
#setenv GREP_OPTIONS '--binary-files=without-match' <- brew doctor says no
setenv LESS '--tabs=2 -iFMRX'
setenv RSYNC_RSH $CVS_RSH

#
# shell interactivity
#
if($?USER && $?prompt && -l $HOME/.tcshrc) then
  #this file is a symlink from ~/.tcshrc to <somedir>/dot-tcsrc
  #source <somedir>/tcsh/*.tcsh
  set _mysrcs = `readlink $HOME/.tcshrc`
  set _mysrcs = $_mysrcs:h/tcsh
  source $_mysrcs/terminal.tcsh

  #do this after terminal to override prompt; before completions/aliases for $path
  if(-d /home/y/bin) source $_mysrcs/y.tcsh

  source $_mysrcs/aliases.tcsh
  source $_mysrcs/completions.tcsh
  unset $_mysrcs
endif

# see also
# /usr/share/tcsh/examples/tcsh.defaults
# http://www.opensource.apple.com/source/tcsh/tcsh-63.2/tcsh/complete.tcsh
