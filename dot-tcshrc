#
# env vars
#
set path=(/{,s}bin /usr/{,s,local/}bin)

#my scripts
if(-d /opt/isao) set path=(/opt/isao $path)

#macports
#if(-d /opt/local) set path=(/opt/local/{,s,apache2/}bin $path)

#homebrew
if(-d /opt/brew) then
  set path=(/opt/brew/bin $path)
  if(-x /opt/brew/bin/node) then
    setenv NODE_PATH /opt/brew/lib/node_modules
    set path=(node_modules/.bin $path)
  endif
endif

setenv EDITOR `which bbedit..sh bbedit nano vim vi |grep ^/ |head -1`
setenv LESS '--tabs=2 -iFMRX'
setenv RSYNC_RSH ssh

#
# shell interactivity
#
if($?USER && $?prompt && -l $HOME/.tcshrc) then
  #this file is a symlink from ~/.tcshrc to <somedir>/dot-tcsrc
  #source <somedir>/tcsh/*.tcsh
  set _dir_ = `readlink $HOME/.tcshrc`
  source $_dir_:h/tcsh/aliases.tcsh
  source $_dir_:h/tcsh/completions.tcsh
  source $_dir_:h/tcsh/y.tcsh
  source $_dir_:h/tcsh/terminal.tcsh
  unset _dir_
endif

# see also
# /usr/share/tcsh/examples/tcsh.defaults
# http://www.opensource.apple.com/source/tcsh/tcsh-63.2/tcsh/complete.tcsh
