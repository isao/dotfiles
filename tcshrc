#
#   env vars
#
set path=(/usr/{local/,local/s}bin /{usr/,,usr/s,s}bin)
setenv EDITOR `which bbedit..sh bbedit nano vim vi |grep ^/ |head -1`
setenv LESS '--tabs=2 -iFMRX'
setenv GREP_COLOR '32' # ANSI/VT100: 32 is green, '1;34' is bold blue
setenv RSYNC_RSH ssh

#
#   shell interactivity
#
if($?USER && $?prompt && -l $HOME/.tcshrc) then
  #this file is a symlink from ~/.tcshrc to <somedir>/dot-tcsrc
  #source <somedir>/tcsh/*.tcsh
  set dotfiles = `readlink $HOME/.tcshrc`
  set dotfiles = $dotfiles:h

  # helper scripts cloned from https://github.com/isao/shell
  set shellrepo = ~/repos/shell

  source "$dotfiles/tcsh.d/aliases.tcsh"
  source "$dotfiles/tcsh.d/git.tcsh"
  source "$dotfiles/tcsh.d/fzf.tcsh"
  source "$dotfiles/tcsh.d/node.tcsh"
  source "$dotfiles/tcsh.d/completions.tcsh"
  source "$dotfiles/tcsh.d/terminal.tcsh"
endif

# see also
# /usr/share/tcsh/examples/tcsh.defaults
# http://www.opensource.apple.com/source/tcsh/tcsh-63.2/tcsh/complete.tcsh
